 config {
  type: "view",
  schema: "DATAFORM_XERO",
  tags: ["staging","daily"]

}

WITH INVOICE_HEADER AS (
  SELECT 
  * 
  FROM  	
    ${ref("V_INVOICE_HEADER_STG")}
),
INVOICE_LINE AS (
  SELECT
  *
  FROM
    ${ref("V_INVOICE_LINE_STG")}
),
rename AS 
(  
SELECT
    --DLHK
    IL.K_INVOICE_LINE_DLHK
    ,S.K_INVOICE_DLHK
    ,S.K_CONTACT_DLHK
    ,S.K_BRANDING_THEME_DLHK
    ,IL.K_ACCOUNT_DLHK
    ,IL.K_ITEM_DLHK
    --BUSINESS KEYS
    ,IL.K_INVOICE_LINE_BK
    ,S.K_INVOICE_BK
    ,S.K_BRANDING_THEME_BK
    ,S.K_CONTACT_BK        
    --ATTRIBUTES
    ,S.A_BRANDING_THEME_NAME  
    ,S.A_CURRENCY_CODE
    ,S.A_DUE_DATE
    ,S.A_EXPECTED_PAYMENT_DATE
    ,S.A_INVOICE_NUMBER
    ,S.A_INV_DATE
    ,S.A_LINE_AMOUNT_TYPES
    ,S.A_PLANNED_PAYMENT_DATE
    ,S.A_REFERENCE
    ,S.A_STATUS
    ,S.A_TYPE
    ,S.A_UPDATED_DATE_DTS
    ,S.A_URL
    ,S.A_FULLY_PAID_ON_DATE
    ,IL.A_ACCOUNT_CODE
    ,IL.A_DESCRIPTION
    ,IL.A_ITEM_CODE
    ,IL.A_TAX_TYPE
    ,IL.A_VALIDATION_ERRORS
    --BOOLEANS
    ,S.B_HAS_ATTACHMENTS
    ,S.B_HAS_ERRORS
    ,S.B_IS_DISCOUNTED
    ,S.B_SENT_TO_CONTACT            
    ,IL.B_DISCOUNT_ENTERED_AS_PERCENT
    --METRICS    
    ,IL.M_DISCOUNT_RATE
    ,IL.M_LINE_AMOUNT
    ,IL.M_QUANTITY
    ,IL.M_TAX_AMOUNT
    ,IL.M_UNIT_AMOUNT
    --METADATA
    ,CURRENT_TIMESTAMP as MD_LOAD_DTS
    ,(SELECT invocation_id FROM ${ref("H_INVOCATION_ID")}) AS MD_INTGR_ID
FROM
    INVOICE_HEADER S
    LEFT JOIN INVOICE_LINE IL on IL.K_INVOICE_BK = S.K_INVOICE_BK    
)

SELECT * FROM rename