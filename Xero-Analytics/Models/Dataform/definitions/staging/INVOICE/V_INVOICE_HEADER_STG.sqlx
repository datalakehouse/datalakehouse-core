 config {
  type: "view",
  schema: "DATAFORM_XERO",
  tags: ["staging","daily"]

}

WITH source AS (
  SELECT 
  * 
  FROM  	
    ${ref("DEMO_XERO","INVOICE")}
),
contacts AS (
  SELECT 
  * 
  FROM  	
    ${ref("W_CONTACTS_D")}
),
branding_theme AS (
  SELECT 
  * 
  FROM  	
    ${ref("V_BRANDING_THEME_STG")}
),
rename AS 
(  
SELECT
    --DLHK
    MD5( TRIM(COALESCE(S.INVOICE_ID,'00000000000000000000000000000000')) ) AS K_INVOICE_DLHK
    ,C.K_CONTACT_DLHK
    ,B.K_BRANDING_THEME_DLHK
    --BUSINESS KEYS
    ,S.BRANDING_THEME_ID AS K_BRANDING_THEME_BK
    ,S.CONTACT_ID AS K_CONTACT_BK    
    ,S.INVOICE_ID AS K_INVOICE_BK
    --ATTRIBUTES
    ,B.A_BRANDING_THEME_NAME  
    ,S.CURRENCY_CODE AS A_CURRENCY_CODE
    ,S.DUE_DATE AS A_DUE_DATE
    ,S.EXPECTED_PAYMENT_DATE AS A_EXPECTED_PAYMENT_DATE
    ,S.INVOICE_NUMBER AS A_INVOICE_NUMBER
    ,S.INV_DATE AS A_INV_DATE
    ,S.LINE_AMOUNT_TYPES AS A_LINE_AMOUNT_TYPES
    ,S.PLANNED_PAYMENT_DATE AS A_PLANNED_PAYMENT_DATE
    ,S.REFERENCE AS A_REFERENCE
    ,S.STATUS AS A_STATUS
    ,S.TYPE AS A_TYPE
    ,S.UPDATED_DATE_UTC AS A_UPDATED_DATE_DTS
    ,S.URL AS A_URL
    ,S.FULLY_PAID_ON_DATE AS A_FULLY_PAID_ON_DATE
    --BOOLEANS
    ,S.HAS_ATTACHMENTS AS B_HAS_ATTACHMENTS
    ,S.HAS_ERRORS AS B_HAS_ERRORS
    ,S.IS_DISCOUNTED AS B_IS_DISCOUNTED
    ,S.SENT_TO_CONTACT AS B_SENT_TO_CONTACT
    --METRICS
    ,S.AMOUNT_PAID::DECIMAL(15,2) AS M_AMOUNT_PAID
    ,S.AMOUNT_CREDITED::DECIMAL(15,2) AS M_AMOUNT_CREDITED
    ,S.AMOUNT_DUE::DECIMAL(15,2) AS M_AMOUNT_DUE
    ,S.CISDEDUCTION::DECIMAL(15,2) AS M_CISDEDUCTION
    ,S.CURRENCY_RATE::DECIMAL(15,2) AS M_CURRENCY_RATE
    ,S.SUB_TOTAL::DECIMAL(15,2) AS M_SUB_TOTAL
    ,S.TOTAL::DECIMAL(15,2) AS M_TOTAL
    ,S.TOTAL_TAX::DECIMAL(15,2) AS M_TOTAL_TAX

    --METADATA
    ,CURRENT_TIMESTAMP as MD_LOAD_DTS
    ,(SELECT invocation_id FROM ${ref("H_INVOCATION_ID")}) AS MD_INTGR_ID
FROM
    source S
    left join contacts C on C.K_CONTACT_BK = S.CONTACT_ID
    left join branding_theme B ON B.K_BRANDING_THEME_BK = S.BRANDING_THEME_ID
)

SELECT * FROM rename