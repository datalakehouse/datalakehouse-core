 config {
  type: "view",
  schema: "DATAFORM_XERO",
  tags: ["staging","daily"]

}

WITH source AS (
  SELECT 
  * 
  FROM  	
    ${ref("DEMO_XERO","PURCHASE_ORDER")}
),
contacts AS (
  SELECT
  *
  FROM
    ${ref("W_CONTACTS_D")}
),
branding_theme AS (
  SELECT
  *
  FROM
    ${ref("V_BRANDING_THEME_STG")}
),
rename AS 
(  
SELECT
    --DLHK
    MD5( TRIM(COALESCE(S.PURCHASE_ORDER_ID,'00000000000000000000000000000000')) ) AS K_PURCHASE_ORDER_DLHK
    ,C.K_CONTACT_DLHK
    ,B.K_BRANDING_THEME_DLHK
    --BUSINESS KEYS
    ,S.PURCHASE_ORDER_ID AS K_PURCHASE_ORDER_BK
    ,S.BRANDING_THEME_ID AS K_BRANDING_THEME_BK
    ,S.CONTACT_ID AS K_CONTACT_BK
    --ATTRIBUTES
    ,S.ATTENTION_TO AS A_ATTENTION_TO
    ,S.CURRENCY_CODE AS A_CURRENCY_CODE
    ,S.DELIVERY_ADDRESS AS A_DELIVERY_ADDRESS
    ,S.DELIVERY_DATE AS A_DELIVERY_DATE
    ,S.DELIVERY_INSTRUCTIONS AS A_DELIVERY_INSTRUCTIONS
    ,S.LINE_AMOUNT_TYPES AS A_LINE_AMOUNT_TYPES
    ,S.PO_DATE AS A_PO_DATE
    ,S.PURCHASE_ORDER_NUMBER AS A_PURCHASE_ORDER_NUMBER
    ,S.REFERENCE AS A_REFERENCE
    ,S.STATUS AS A_STATUS
    ,S.TELEPHONE AS A_TELEPHONE
    ,S.TYPE AS A_TYPE
    ,S.UPDATED_DATE_UTC AS A_UPDATED_DATE_DTS 

    --metrics

    --BOOLEAN
    ,S.IS_DISCOUNTED AS B_IS_DISCOUNTED
    --METRICS
    ,S.CURRENCY_RATE AS M_CURRENCY_RATE
    ,S.SUB_TOTAL AS M_SUB_TOTAL
    ,S.TOTAL AS M_TOTAL
    ,S.TOTAL_TAX AS M_TOTAL_TAX


    --METADATA
    ,CURRENT_TIMESTAMP as MD_LOAD_DTS
    ,(SELECT invocation_id FROM ${ref("H_INVOCATION_ID")}) AS MD_INTGR_ID
FROM
    source S
    LEFT JOIN contacts C on C.K_CONTACT_BK = S.CONTACT_ID    
    LEFT JOIN branding_theme B ON B.K_BRANDING_THEME_BK = S.BRANDING_THEME_ID
)

SELECT * FROM rename