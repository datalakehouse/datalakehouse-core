 config {
  type: "view",
  schema: "DATAFORM_QUICKBOOKS",
  tags: ["staging", "daily"]

}

WITH source AS(
    SELECT * FROM  ${ref("DEMO_QUICKBOOKS","PURCHASE")}
),
vendors AS (
    SELECT * FROM ${ref("W_VENDORS_D")}
),
accounts AS (
    SELECT * FROM ${ref("W_ACCOUNTS_D")}
),
currency AS (
    SELECT * FROM ${ref("W_CURRENCY_D")}
),
customers AS (
    SELECT * FROM ${ref("W_CUSTOMERS_D")}
),
employees AS (
    SELECT * FROM ${ref("W_EMPLOYEES_D")}
),
rename AS (
SELECT
--DLHK
MD5( TRIM(COALESCE(P.ID,'00000000000000000000000000000000')) ) AS K_PURCHASE_DLHK
,v.K_VENDOR_DLHK
,a.K_ACCOUNT_DLHK
,c.K_CURRENCY_DLHK
,cus.K_CUSTOMER_DLHK
,e.K_EMPLOYEE_DLHK
--BK
,P.ID AS K_PURCHASE_BK
,P.CURRENCY_ID AS K_CURRENCY_BK
,P.ACCOUNT_ID AS K_ACCOUNT_BK
,P.DEPARTMENT_ID AS K_DEPARTMENT_BK
,P.PAYMENT_METHOD_ID AS K_PAYMENT_METHOD_BK
,P.CUSTOMER_ID AS K_CUSTOMER_BK
,P.VENDOR_ID AS K_VENDOR_BK
,P.EMPLOYEE_ID AS K_EMPLOYEE_BK
,P.TAX_CODE_ID AS K_TAX_CODE_BK

--ATTRIBUTES
,P.SYNC_TOKEN AS A_SYNC_TOKEN
,P.DOC_NUMBER AS A_DOC_NUMBER
,P.PAYMENT_TYPE AS A_PAYMENT_TYPE
,P.PRINT_STATUS AS A_PRINT_STATUS
,P.PRIVATE_NOTE AS A_PRIVATE_NOTE
,P.GLOBAL_TAX_CALCULATION AS A_GLOBAL_TAX_CALCULATION
,P.CREATED_AT AS A_CREATED_AT_DTS
,P.UPDATED_AT AS A_UPDATED_AT_DTS
,P.REMIT_TO_ADDRESS_ID AS A_REMIT_TO_ADDRESS_ID
,P.TRANSACTION_DATE AS A_TRANSACTION_DATE
,P.TRANSACTION_SOURCE AS A_TRANSACTION_SOURCE

--BOOLEAN
,P.CREDIT AS B_CREDIT

--METRICS
,P.EXCHANGE_RATE::DECIMAL(15,2) AS M_EXCHANGE_RATE
,P.TOTAL_TAX AS M_TOTAL_TAX
,P.TOTAL_AMOUNT AS M_TOTAL_AMOUNT

 --METADATA
,CURRENT_TIMESTAMP as MD_LOAD_DTS
,(SELECT invocation_id FROM ${ref("H_INVOCATION_ID")}) AS MD_INTGR_ID

FROM
source as P
LEFT JOIN vendors as v on v.K_VENDOR_BK = P.VENDOR_ID
LEFT JOIN accounts AS a on a.K_ACCOUNT_BK = P.ACCOUNT_ID
LEFT JOIN currency AS c on c.K_CURRENCY_BK = P.CURRENCY_ID
LEFT JOIN customers AS cus on cus.K_CUSTOMER_BK = P.CUSTOMER_ID
LEFT JOIN employees AS e on e.K_EMPLOYEE_BK = P.EMPLOYEE_ID
)

SELECT * FROM rename

