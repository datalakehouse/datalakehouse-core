 config {
  type: "view",
  schema: "DATAFORM_QUICKBOOKS",
  tags: ["staging","daily"]

}

WITH source AS (
  SELECT 
  * 
  FROM  	
    ${ref("DEMO_QUICKBOOKS","ACCOUNT")}
),
rename AS 
(   
SELECT 
    --MD5
    MD5( TRIM(COALESCE(A.ID,'00000000000000000000000000000000')) ) AS K_ACCOUNT_DLHK
    --BUSINESS KEYS
    ,A.ID AS K_ACCOUNT_BK
    ,A.CURRENCY_ID AS K_CURRENCY_BK
    ,A.PARENT_ACCOUNT_ID AS K_PARENT_ACCOUNT_BK
    --ATTRIBUTES    
    ,A."DESCRIPTION" AS A_DESCRIPTION
    ,A.FULLY_QUALIFIED_NAME AS A_FULLY_QUALIFIED_NAME
    ,regexp_count(A.FULLY_QUALIFIED_NAME, '\\b:\\b', 1) AS A_ACCOUNT_LEVEL
    ,NULLIF(SPLIT_PART(A.FULLY_QUALIFIED_NAME,':',1),'') AS A_FIRST_LEVEL_ACCOUNT_NAME
    ,NULLIF(SPLIT_PART(A.FULLY_QUALIFIED_NAME,':',2),'') AS A_SECOND_LEVEL_ACCOUNT_NAME
    ,NULLIF(SPLIT_PART(A.FULLY_QUALIFIED_NAME,':',3),'') AS A_THIRD_LEVEL_ACCOUNT_NAME
    ,NULLIF(SPLIT_PART(A.FULLY_QUALIFIED_NAME,':',4),'') AS A_FOURTH_LEVEL_ACCOUNT_NAME
    ,NULLIF(SPLIT_PART(A.FULLY_QUALIFIED_NAME,':',5),'') AS A_FIFTH_LEVEL_ACCOUNT_NAME
    ,NULLIF(SPLIT_PART(A.FULLY_QUALIFIED_NAME,':',6),'') AS A_SIXTH_LEVEL_ACCOUNT_NAME   
    ,A.ACCOUNT_TYPE AS A_ACCOUNT_TYPE
    ,A.ACCOUNT_SUB_TYPE AS A_ACCOUNT_SUB_TYPE
    ,A."NAME" AS A_ACCOUNT_NAME
    ,A.ACTIVE AS A_ACTIVE
    ,A.CLASSIFICATION AS A_CLASSIFICATION    
    ,A.ACCOUNT_NUMBER AS A_ACCOUNT_NUMBER
    ,CASE
        WHEN A.PARENT_ACCOUNT_ID = A.ID 
            THEN A.ACCOUNT_NUMBER 
            ELSE B.ACCOUNT_NUMBER 
        END AS A_PARENT_ACCOUNT_NUMBER
    --TIMESTAMP
    ,A.CREATED_AT AS A_CREATED_AT_DTS
    ,A.UPDATED_AT AS A_UPDATED_AT_DTS
    ,A.SYNC_TOKEN AS B_SYNC_TOKEN
    --METRICS
    ,A.BALANCE_WITH_SUB_ACCOUNTS::decimal(15,2) AS M_BALANCE_WITH_SUB_ACCOUNTS
    ,A.BALANCE::decimal(15,2) AS M_BALANCE    
    --METADATA
    ,CURRENT_TIMESTAMP as MD_LOAD_DTS
    ,(SELECT invocation_id FROM ${ref("H_INVOCATION_ID")}) AS MD_INTGR_ID
  FROM
    source  A
    left join source B on B.ID = A.PARENT_ACCOUNT_ID    
)


SELECT * FROM rename