 config {
  type: "view",
  schema: "DATAFORM_QUICKBOOKS",
  tags: ["staging", "daily"]

}

WITH source AS (
  SELECT 
  * 
  FROM  	
    ${ref("DEMO_QUICKBOOKS_SANDBOX","INVOICE")}
),
accounts AS (
    SELECT * FROM ${ref("W_ACCOUNTS_D")}
),
currency AS (
    SELECT * FROM ${ref("W_CURRENCY_D")}
),
class AS (
    SELECT * FROM ${ref("W_CLASS_D")}
),
customers AS (
    SELECT * FROM ${ref("W_CUSTOMERS_D")}
),
term AS (
  SELECT  *  FROM  ${ref("W_TERM_D")}
),
payments AS (
  SELECT  *  FROM  ${ref("W_PAYMENTS_F")}
),
invoice_linked AS (
  SELECT  *  FROM  ${ref("DEMO_QUICKBOOKS_SANDBOX","INVOICE_LINKED_TXN")}
),
invoice_payment AS (
  SELECT 
    IL.INVOICE_ID,
    SUM(P.M_AMOUNT) PAYMENT_AMOUNT,
    MAX(P.A_TRANSACTION_DATE) AS TRANSACTION_DATE,
    MAX(P.A_TRANSACTION_STATUS) AS TRANSACTION_STATUS
  FROM 
  invoice_linked IL
  INNER JOIN source S on S.ID = IL.INVOICE_ID 
  INNER JOIN payments P ON P.K_PAYMENT_BK = IL.PAYMENT_ID
  GROUP BY 
  IL.INVOICE_ID
),
rename AS 
(   
SELECT
    --DLHK
    MD5( TRIM(COALESCE(I.ID,'00000000000000000000000000000000')) ) AS K_INVOICE_DLHK
    ,T.K_TERM_DLHK
    ,C.K_CURRENCY_DLHK
    ,CUS.K_CUSTOMER_DLHK
    ,A.K_ACCOUNT_DLHK AS K_DEPOSIT_TO_ACCOUNT_DLHK
    --BK
    ,I.ID AS K_INVOICE_BK
    ,I.SALES_TERM_ID AS K_SALES_TERM_BK
    ,I.BILLING_ADDRESS_ID AS K_BILLING_ADDRESS_BK
    ,I.CURRENCY_ID AS K_CURRENCY_BK
    ,I.CLASS_ID AS K_CLASS_BK
    ,I.DEPARTMENT_ID AS K_DEPARTMENT_BK
    ,I.CUSTOMER_ID AS K_CUSTOMER_BK
    ,I.SHIPPING_ADDRESS_ID AS K_SHIPPING_ADDRESS_BK
    ,I.TAX_CODE_ID AS K_TAX_CODE_BK
    ,I.DEPOSIT_TO_ACCOUNT_ID AS K_DEPOSIT_TO_ACCOUNT_BK       
    --ATTRIBUTES    
    ,I.DOC_NUMBER AS A_DOC_NUMBER
    ,I.TRANSACTION_DATE AS A_TRANSACTION_DATE
    ,I.BILLING_BCC_EMAIL AS A_BILLING_BCC_EMAIL
    ,I.TRANSACTION_SOURCE AS A_TRANSACTION_SOURCE  
    
    ,I.BILLING_EMAIL AS A_BILLING_EMAIL
    ,I.SYNC_TOKEN AS A_SYNC_TOKEN
    ,I.CREATED_AT AS A_CREATED_AT_DTS
    ,I.UPDATED_AT AS A_UPDATED_AT_DTS
    ,I.SHIP_DATE AS A_SHIP_DATE
    ,I.GLOBAL_TAX_CALCULATION AS A_GLOBAL_TAX_CALCULATION    
    ,I.DELIVERY_TYPE AS A_DELIVERY_TYPE    
    ,I.PRINT_STATUS AS A_PRINT_STATUS
    ,I.DELIVERY_TIME AS A_DELIVERY_TIME
    ,I.BILLING_CC_EMAIL AS A_BILLING_CC_EMAIL
    ,I.CUSTOMER_MEMO AS A_CUSTOMER_MEMO
    ,I.DUE_DATE AS A_DUE_DATE
    ,I.EMAIL_STATUS AS A_EMAIL_STATUS    
    ,I.PRIVATE_NOTE AS A_PRIVATE_NOTE
    ,I.TRACKING_NUMBER AS A_TRACKING_NUMBER 
    ,IP.TRANSACTION_DATE AS A_PAYMENT_DATE
    ,IP.TRANSACTION_STATUS AS A_PAYMENT_STATUS
    --METRICS
    ,IP.PAYMENT_AMOUNT::DECIMAL(15,2) AS A_PAYMENT_AMOUNT
    ,I.DEPOSIT::DECIMAL(15,2) AS M_DEPOSIT
    ,I.TOTAL_AMOUNT::DECIMAL(15,2) AS M_TOTAL_AMOUNT
    ,I.EXCHANGE_RATE::DECIMAL(15,2) AS M_EXCHANGE_RATE    
    ,I.TOTAL_TAX::DECIMAL(15,2) AS M_TOTAL_TAX
    ,I.HOME_TOTAL_AMOUNT::DECIMAL(15,2) AS M_HOME_TOTAL_AMOUNT
    ,I.BALANCE::DECIMAL(15,2) AS M_BALANCE
    ,I.HOME_BALANCE::DECIMAL(15,2) AS M_HOME_BALANCE      
    --BOLEAN
    ,I.ALLOW_IPNPAYMENT AS B_ALLOW_IPNPAYMENT
    ,I.ALLOW_ONLINE_ACHPAYMENT AS B_ALLOW_ONLINE_ACHPAYMENT
    ,I.APPLY_TAX_AFTER_DISCOUNT AS B_APPLY_TAX_AFTER_DISCOUNT
    ,I.ALLOW_ONLINE_CREDIT_CARD_PAYMENT AS B_ALLOW_ONLINE_CREDIT_CARD_PAYMENT
    ,I.ALLOW_ONLINE_PAYMENT AS B_ALLOW_ONLINE_PAYMENT
    --METADATA
    ,CURRENT_TIMESTAMP as MD_LOAD_DTS
    ,(SELECT invocation_id FROM ${ref("H_INVOCATION_ID")}) AS MD_INTGR_ID 
FROM  
    source  I
    LEFT JOIN currency C ON C.K_CURRENCY_BK = I.CURRENCY_ID
    LEFT JOIN term T ON t.K_TERM_BK = I.SALES_TERM_ID
    LEFT JOIN customers CUS ON CUS.K_CUSTOMER_BK = I.CUSTOMER_ID
    LEFT JOIN class CLS on CLS.K_CLASS_BK = I.CLASS_ID
    LEFT JOIN accounts A ON A.K_ACCOUNT_BK = I.DEPOSIT_TO_ACCOUNT_ID
    LEFT JOIN invoice_payment IP ON IP.INVOICE_ID = I.ID
)


SELECT * FROM rename