 config {
  type: "view",
  schema: "DATAFORM_SQUARE",
  tags: ["staging", "daily"]

}

WITH source AS (
  SELECT * FROM  ${ref("DEMO_SQUARE_ALT13","ORDER")} AS A
),
rename AS 
(
SELECT 
--MD5 KEYS
  MD5( TRIM(ID) ) AS K_POS_ORDER_DLHK
  ,MD5( TRIM(COALESCE(LOCATION_ID,'00000000000000000000000000000000') )) AS K_POS_LOCATION_DLHK
  ,MD5( TRIM(COALESCE(REFERENCE_ID,'00000000000000000000000000000000' ) )) AS K_POS_REFERENCE_DLHK
  ,MD5( TRIM(COALESCE(CUSTOMER_ID,'00000000000000000000000000000000') )) AS K_POS_CUSTOMER_DLHK  
  ,MD5( TRIM(COALESCE(TOTAL_MONEY_CURRENCY,'00000000000000000000000000000000') )) AS K_CURRENCY_DLHK  
--BUSINESS KEYS
  ,ID AS K_POS_ORDER_BK
  ,LOCATION_ID AS K_POS_LOCATION_BK
  ,CUSTOMER_ID AS K_POS_CUSTOMER_BK
  ,REFERENCE_ID AS K_POS_REFERENCE_BK
--TIMESTAMP FIELDS
  ,CREATED_AT AS A_ORDER_CREATED_DTS
  ,UPDATED_AT AS A_ORDER_UPDATED_DTS
  ,CLOSED_AT AS A_ORDER_CLOSED_DTS  
  
--ATTRIBUTES AND METRICS
  ,STATE AS A_ORDER_STATE
  ,ORDER_SOURCE_NAME AS A_ORDER_SOURCE    
  ,ROUND(NVL(NET_AMOUNT_TIP_MONEY_AMOUNT - RETURN_AMOUNT_TIP_MONEY_AMOUNT, 000)/100, 2)::decimal(15,2) AS M_NET_TIP_MONEY_AMT  
  ,TOTAL_SERVICE_CHARGE_AMOUNT AS M_TOTAL_SERVICE_CHARGE_AMT  
  ,ROUND(NVL(RETURN_AMOUNT_TOTAL_MONEY_AMOUNT, 000)/100, 2)::decimal(15,2) AS M_RETURN_TOTAL_AMT
  ,ROUND(NVL(NET_AMOUNT_TOTAL_MONEY_AMOUNT, 000)/100, 2)::decimal(15,2) AS M_NET_TOTAL_AMT
  ,ROUND(NVL(TOTAL_MONEY_AMOUNT, 000)/100, 2)::decimal(15,2) AS M_TOTAL_AMT
  ,ROUND(NVL(TOTAL_TAX_AMOUNT, 000)/100, 2)::decimal(15,2) AS M_TAX_TOTAL_AMT
  ,ROUND(NVL(TOTAL_DISCOUNT_AMOUNT, 000)/100, 2)::decimal(15,2) AS M_DISCOUNT_TOTAL_AMT
  
  --METADATA (MD)
  ,CURRENT_TIMESTAMP as MD_LOAD_DTS
  ,(SELECT invocation_id FROM ${ref("H_INVOCATION_ID")}) AS MD_INTGR_ID
FROM source
)

SELECT * FROM rename